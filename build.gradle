import java.nio.charset.StandardCharsets
import java.time.Duration

/*
 * MIT License
 *
 * Copyright (c) 2021 Justin Heflin
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 *
 */

plugins {
    id 'idea'
    id 'base'
    id 'org.sonarqube' version '3.0'
    id 'de.marcphilipp.nexus-publish' version '0.4.0'
}

sonarqube {
    properties {
        property "sonar.projectKey", "heretere_hdl"
        property "sonar.organization", "heretere"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

allprojects {
    group 'com.heretere.hdl'
    version '1.3.1'
    description = 'Runtime dependency loading library'
}

task GHPagesJavadoc(type: Javadoc) {
    afterEvaluate {
        title "Heretere's Dependency Loader"
        source rootProject.subprojects.collect { it.sourceSets.main.allJava }
        classpath = files(rootProject.subprojects.collect { it.sourceSets.main.compileClasspath })
        options {
            links 'https://docs.oracle.com/en/java/javase/11/docs/api',
                'https://javadoc.io/doc/org.jetbrains/annotations/20.1.0',
                'https://hub.spigotmc.org/javadocs/spigot/'
        }
        destinationDir file(rootProject.buildDir.getParent() + '/docs/v' + rootProject.version + '/')
    }
}

subprojects { subproject ->
    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'de.marcphilipp.nexus-publish'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceCompatibility = '1.8'

    [JavaCompile, Javadoc].forEach { taskType ->
        tasks.withType(taskType) {
            options.encoding(StandardCharsets.UTF_8.name())
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    nexusPublishing {
        connectTimeout.set(Duration.ofMinutes(10))
        clientTimeout.set(Duration.ofMinutes(10))
        repositories {
            sonatype()
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    name = "Heretere's Dependency Loader"
                    artifactId = subproject.name
                    packaging = 'jar'
                    url = 'https://github.com/heretere/hdl'
                    description = subproject.description

                    developers {
                        developer {
                            id = 'Heretere'
                            name = 'Justin Heflin'
                            url = 'https://heretere.com'
                            email = 'heirteir@protonmail.com'
                        }
                    }

                    issueManagement {
                        system = 'GitHub Issues'
                        url = 'https://github.com/heretere/hdl/issues'
                    }

                    scm {
                        connection = 'scm:git@github.com:heretere/hdl.git'
                        developerConnection = 'scm:git@github.com:heretere/hdl.git'
                        url = 'https://github.com/heretere/hdl'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                }
            }
        }

        signing {
            useGpgCmd()
            sign publishing.publications.mavenJava
        }
    }

    javadoc {
        title = subproject.name
        source subproject.sourceSets.main.allJava
        classpath = subproject.sourceSets.main.compileClasspath
        options {
            links 'https://docs.oracle.com/en/java/javase/11/docs/api',
                'https://javadoc.io/doc/org.jetbrains/annotations/20.1.0',
                'https://hub.spigotmc.org/javadocs/spigot/'
        }
    }
}

wrapper {
    gradleVersion = "6.8-rc-1"
    distributionType = Wrapper.DistributionType.ALL
}