/*
 * MIT License
 *
 * Copyright (c) 2021 Justin Heflin
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 *
 */

plugins {
    id 'idea'
    id 'base'
    id 'org.sonarqube' version '3.0'
}

group 'com.github.heretere'
version '1.3.1'

sonarqube {
    properties {
        property "sonar.projectKey", "heretere_hdl"
        property "sonar.organization", "heretere"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

def docProjects = [
    ':core',
    ':spigot'
]

subprojects { subproject ->
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        options.encoding = "UTF-8"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set("javadoc")
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    url = 'https://github.com/heretere/hdl'

                    developers {
                        developer {
                            id = 'Heretere'
                            name = 'Justin Heflin'
                        }
                    }

                    issueManagement {
                        system = 'GitHub Issues'
                        url = 'https://github.com/heretere/hdl/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                }
            }
        }
    }

    afterEvaluate {
        javadoc {
            title "Heretere's Dependency Loader"
            source docProjects.collect { project(it).sourceSets.main.allJava }
            classpath = files(docProjects.collect { project(it).sourceSets.main.compileClasspath })
            options {
                links 'https://docs.oracle.com/en/java/javase/11/docs/api',
                    'https://javadoc.io/doc/org.jetbrains/annotations/20.1.0',
                    'https://hub.spigotmc.org/javadocs/spigot/'
            }
            destinationDir file(rootProject.buildDir.getParent() + '/docs/v' + rootProject.version + '/')
        }
    }
}

wrapper {
    gradleVersion = "6.8-rc-1"
    distributionType = Wrapper.DistributionType.ALL
}